buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.2"
    }
}
apply plugin: "com.gradle.plugin-publish"
apply plugin: 'java-gradle-plugin'
apply plugin: 'groovy'

group = 'net.ltgt.gradle'

gradle.taskGraph.whenReady { graph ->
    if (graph.hasTask(publishPlugins)) {
        assert JavaVersion.current() == JavaVersion.VERSION_1_7 : "Releases must be built with JDK 7 (make sure to clean and rebuild after changing JDK)"

        assert 'git diff --quiet --exit-code'.execute(null, rootDir).waitFor() == 0 : "Working tree is dirty"
        def process ='git describe --exact-match'.execute(null, rootDir)
        assert process.waitFor() == 0 : "Version is not tagged"
        version = process.text.trim() - ~'^v'
    }
}

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    testCompile('com.netflix.nebula:nebula-test:4.2.2')
    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude group: 'org.codehaus.groovy'
    }
    testCompile gradleTestKit()
}

wrapper {
  gradleVersion = "2.14"
}

test {
    inputs.file tasks.jar
    systemProperty("plugin", tasks.jar.archivePath)
    if (project.hasProperty("test.gradle-versions")) {
        systemProperty("test.gradle-versions", project.property("test.gradle-versions"))
    }
    testLogging {
        showExceptions = true
        showStackTraces = true
        exceptionFormat = 'FULL'
    }
}

pluginBundle {
    website = 'https://github.com/tbroyer/gradle-apt-plugin'
    vcsUrl = 'https://github.com/tbroyer/gradle-apt-plugin'
    description = 'Gradle plugin making it easier/safer to use Java annotation processors'
    tags = [ 'annotation-processing', 'annotation-processors', 'apt' ]

    plugins {
        aptPlugin {
            id = 'net.ltgt.apt'
            displayName = 'Gradle APT plugin'
        }
    }

    mavenCoordinates {
        groupId = project.group
        artifactId = project.name
    }
}
